package proyectofinal.ctdoa.backend.analizador.python;

import java_cup.runtime.*;
import proyectofinal.ctdoa.backend.objetos.*;
import proyectofinal.ctdoa.backend.manejadores.*;
import java.util.ArrayList;
import java.util.List;
import proyectofinal.ctdoa.frontend.gui.FrameCTDOA;

parser code {:

    public ManejadorSintacticoVB_PY msVB_PY = ManejadorSintacticoVB_PY.getInstacia();
    public ManejadorInstancias mi = ManejadorInstancias.getInstance();
    public ManejadorCuartetos mc = ManejadorCuartetos.getInstancia();
    public ManejadorBuscarMetodos mbm = ManejadorBuscarMetodos.getInstancia();
    public ManejadorReturn mr = ManejadorReturn.getInstancia();
    public int contadorLineas;
    public LexicoPython lexer;
    private FrameCTDOA frameCTDOA;
    
    public void setContadorLineas(int contadorLineas){
        this.contadorLineas = contadorLineas;
        msVB_PY.setContadorLineas(contadorLineas);
    }

    public SintacticoPython(LexicoPython lexer) { 
        super(lexer);   
        this.lexer = lexer;
        msVB_PY.nuevoAnalisis();
    }

    public void setLinea(int contadorLineas){
        this.contadorLineas = contadorLineas;
    }

    public void setFrameCTDOA(FrameCTDOA frameCTDOA) {
        this.frameCTDOA = frameCTDOA;
        msVB_PY.setFrameCTDOA(frameCTDOA);
    }

    public void setAccion(String tipo, String mensaje, String accion){
        frameCTDOA.addErrores("ERROR Lexico Tipo: " + tipo + "\t Mensaje: " + mensaje + "\tAcci√≥n: ERROR\n");
    }

    @Override
    public void syntax_error(Symbol s){
        try {
            msVB_PY.errorSintax(s.left, s.right, s.value, 
                "Uf, error sintactico en el codigo Python, cerca de la linea y columna indicada porfavor revisa que el token\n "
                        + "\t\tidicado corresponda a la declaracion de instruccion correspondiente. Vuelve a intentarlo."
            );
        } catch (Exception e) {
            System.out.println("Error " + e.toString());
        }
    }
:}
    

/* Terminales */

terminal            CADENA, CARACTER;
terminal            MAS, MENOS, POR, MOD, DIV;
terminal            IGUAL;
terminal            DISTINTO, IGUAL_IGUAL, DIFERENTE, MENOR, MAYOR, MENOR_IGUAL, MAYOR_IGUAL, NOT, AND, OR;
terminal            PRINT, WHILE, FOR, IN, RANGE, IF, ELSE, ELIF, DEF, RETURN;
terminal            COMA, DOS_PUNTOS, PARENTESIS_A, PARENTESIS_C, SALTO;
terminal            IINPUT, FINPUT, CINPUT;
terminal            INDENT, DEDENT;
terminal Integer    NUMERO;
terminal float      REAL;
terminal String     ID;

/* No Terminales */ 

non terminal inicio, ppa;
non terminal estructuras_def, space;
non terminal return, return_dato, def_p;
non terminal operaciones_aritmeticas;
non terminal operaciones_logicas;
non terminal tipos_comparacion;
non terminal condicional, solicitud_p;
non terminal concatenacion, and, or, condicional_or, condicional_and, condicional_not, parametros_p;
non terminal mensaje_terminal;
non terminal asignacion_variables, asignacion;
non terminal solicitud_datos, var, for_p;
non terminal while, estruct, while_n, while_p;
non terminal for, rango, ii, ie, ee;
non terminal if, else, else_if, else_if_p;
non terminal estructura_if, llamada_fp, param, param_p;
non terminal def, parametros, estructuras, estructuras_p;


/* */

precedence left SALTO;
precedence left CINPUT, FINPUT, IINPUT;
precedence left DEDENT;
precedence left RETURN;
precedence left PRINT;
precedence left ID;
precedence left FOR;
precedence left WHILE;
precedence left IF, ELIF, ELSE;
precedence left MAS, MENOS, COMA;
precedence left POR, DIV, MOD;
precedence left OR;
precedence left AND;
precedence left NOT;
precedence left PARENTESIS_A, PARENTESIS_C;

/* Gramaticas */ 

start with inicio;

inicio ::=
    SALTO estructuras_def:e                             {:parser.mi.setClasePY(parser.msVB_PY.clasePY(e));:}
    | estructuras_def:e                                 {:parser.mi.setClasePY(parser.msVB_PY.clasePY(e));:}
;

/********************************** Metodos y Funciones *******************************/

estructuras_def ::=
    def:e estructuras_def:a 
     {:
        List<Simbolo> listaSimbolos = new ArrayList<>();
        if(a != null) {
            if(a instanceof Simbolo) {
                listaSimbolos.add((Simbolo) a);
            } else {
                listaSimbolos = (List<Simbolo>) a;
            }
        }
        if(e != null) {
            listaSimbolos.add((Simbolo) e);
        }
        RESULT = listaSimbolos;
    :}
    |                           {:RESULT = null;:}
;

/******************************** Asignacion Return ***********************************/

return ::=
    RETURN return_dato:e
    {: 
        if(e != null) {
            if (parser.mr.compararReturn((Simbolo) e, eleft, eright)){
                RESULT = new Simbolo(Constantes.RETURN_VAR, e);
            } else {
                RESULT = null;
            }
        } else {
            RESULT = null;
        }    
    :}
    | error:a return_dato:e
    {:  
        parser.msVB_PY.errorSintax(aleft, aright, a, "Error en la sintaxis de la instruccion return. Se esperaba el token << RETURN >>");
        if(e != null) {
            if (parser.mr.compararReturn((Simbolo) e, eleft, eright)){
                RESULT = new Simbolo(Constantes.RETURN_VAR, e);
            } else {
                RESULT = null;
            }
        } else {
            RESULT = null;
        }    
    :}
    | RETURN error:e
    {:parser.msVB_PY.errorSintax(eleft, eright, e, "Error en la sintaxis de la instruccion return. Se esperaba: << Operacion aritmetica, identificador, numero>>.");:}
;

return_dato ::=
    CARACTER:e                              {:RESULT = new Simbolo(Constantes.CHAR_VAR_VB_PY, e);:}
    | operaciones_aritmeticas:e             {:RESULT = e;:}
;

/******************************* Operaciones Aritmeticas ******************************/

operaciones_aritmeticas ::= 
    operaciones_aritmeticas:a MAS operaciones_aritmeticas:e             {:RESULT = parser.msVB_PY.realizarOperaciones(a, e, aleft, aright, eleft, eright, 1);:}       
    | operaciones_aritmeticas:a MENOS operaciones_aritmeticas:e         {:RESULT = parser.msVB_PY.realizarOperaciones(a, e, aleft, aright, eleft, eright, 2);:}
    | operaciones_aritmeticas:a MOD operaciones_aritmeticas:e           {:RESULT = parser.msVB_PY.realizarOperaciones(a, e, aleft, aright, eleft, eright, 5);:}
    | operaciones_aritmeticas:a POR operaciones_aritmeticas:e           {:RESULT = parser.msVB_PY.realizarOperaciones(a, e, aleft, aright, eleft, eright, 3);:}
    | operaciones_aritmeticas:a DIV operaciones_aritmeticas:e           {:RESULT = parser.msVB_PY.realizarOperaciones(a, e, aleft, aright, eleft, eright, 4);:}
    | MENOS estruct:e                                                   {:RESULT = parser.msVB_PY.modificarVariables(e, eleft, eright);:}
    | estruct:e                                                         {:RESULT = e;:} 
;

estruct ::=
    PARENTESIS_A operaciones_aritmeticas:e PARENTESIS_C             {:RESULT = e;:}
    | ID:e                                                          {:RESULT = parser.msVB_PY.metodoBuscarID(e.toString(), eleft, eright);:}
    | NUMERO:e                                                      {:RESULT = new Simbolo(Constantes.INT_VAR_VB_PY, e);:}
    | REAL:e                                                        {:RESULT = new Simbolo(Constantes.FLOAT_VAR_VB_PY, e);:}
    | llamada_fp:a          
    {:
        if(a != null) {
            Tipo tipo = ((Simbolo) a).getTipoFuncion();
            if(tipo != null && tipo.getNombre().equals("Funcion")) {
                RESULT = a;
            } else {
                parser.msVB_PY.errorSemantico(aleft, aright, "Asignacion", "Error, los metodos no pueden asignarse.");
                RESULT = null;
            }
        }
    :}
;

/***************************** Operaciones Logicas ************************************/

operaciones_logicas ::=
    tipos_comparacion:a IGUAL_IGUAL:c tipos_comparacion:e               {:RESULT = parser.msVB_PY.comprobarOperacionesLogicas(a, e, 1, cleft, cright);:}
    | tipos_comparacion:a DISTINTO:c tipos_comparacion:e                {:RESULT = parser.msVB_PY.comprobarOperacionesLogicas(a, e, 2, cleft, cright);:}
    | tipos_comparacion:a DIFERENTE:c tipos_comparacion:e               {:RESULT = parser.msVB_PY.comprobarOperacionesLogicas(a, e, 2, cleft, cright);:}
    | tipos_comparacion:a MENOR:c tipos_comparacion:e                   {:RESULT = parser.msVB_PY.comprobarOperacionesLogicas(a, e, 3, cleft, cright);:}
    | tipos_comparacion:a MENOR_IGUAL:c tipos_comparacion:e             {:RESULT = parser.msVB_PY.comprobarOperacionesLogicas(a, e, 4, cleft, cright);:}
    | tipos_comparacion:a MAYOR:c tipos_comparacion:e                   {:RESULT = parser.msVB_PY.comprobarOperacionesLogicas(a, e, 5, cleft, cright);:}
    | tipos_comparacion:a MAYOR_IGUAL:c tipos_comparacion:e             {:RESULT = parser.msVB_PY.comprobarOperacionesLogicas(a, e, 6, cleft, cright);:}
    | tipos_comparacion:c error:a tipos_comparacion:e             
    {:parser.msVB_PY.errorSintax(aleft, aright, a, "Error sintactico en la instruccion de operaciones de comparacion. Se esperaba algun token como: << <, >, <=.>=, <>, = >>");:}
;

tipos_comparacion ::=
    operaciones_aritmeticas:e                   {:RESULT = e;:}
    | CARACTER:e                                {:RESULT = new Simbolo(Constantes.CHAR_VAR_VB_PY, e);:}
    | error:a
    {:parser.msVB_PY.errorSintax(aleft, aright, a, "Error sintactico en la instruccion de operaciones de comparacion. Se esperaba algun token como: << Identificador, Operacion Aritmetica, Caracter >>");:}
;

/********************************** Condicionales *************************************/

condicional ::=
    condicional_or:e                                        {:parser.mc.nuevoAnalisisBoolean(); RESULT = e;:}
    | error:e SALTO                           
    {:
        parser.mc.nuevoAnalisisBoolean();  
        parser.msVB_PY.errorSintax(eleft, eright, e, "Error sintactico en las operacionaciones logicas, de la instruccion condicional. Se esperaba una operacion condicional como: << OP1 OperadorComparacion OP2 >>");:}
    | error:e INDENT
    {:
        parser.mc.nuevoAnalisisBoolean();  
        parser.msVB_PY.errorSintax(eleft, eright, e, "Error sintactico en las operacionaciones logicas, de la instruccion condicional. Se esperaba una operacion condicional como: << OP1 OperadorComparacion OP2 >>");:}

;

and ::=
    AND             {:parser.mc.and(); parser.mc.condicionalAND(); parser.mc.escribirEtiqueta();:}
;

or ::=
    OR              {:parser.mc.or(); parser.mc.condicionalOR(); if (!parser.mc.ultimoAgregadorEtiqueta()) { parser.mc.escribirEtiqueta();}:}
;

condicional_or ::= 
    condicional_or:a or:c condicional_or:e                      {:RESULT = parser.msVB_PY.comprobacionCondicional(a, e, 2, cleft, cright);:}
    | condicional_and:a                                         {:RESULT = a;:}
    | error:e
    {:parser.msVB_PY.errorSintax(eleft, eright, e, "Error sintactico en las operacionaciones logicas, de la intruccion condicional. Se esperaba la estructura: << OP1 Operador Logico OP2 >>");:}
;

condicional_and ::= 
    condicional_and:a and:c condicional_and:e                   {:RESULT = parser.msVB_PY.comprobacionCondicional(a, e, 1, cleft, cright);:}
    | condicional_not:a                                         {:RESULT = a;:}
;

condicional_not ::=
    NOT ppa operaciones_logicas:a PARENTESIS_C          {:RESULT = parser.msVB_PY.comprobacionCondicional(a, true, 3, aleft, aright);:}
    | NOT ppa operaciones_logicas:a error:e               
    {:
        parser.msVB_PY.errorSintax(eleft, eright, e, "Error sintactico en las operacionaciones logicas, de la intruccion condicional not. Se esperaba el token: << ) >>");
        RESULT = parser.msVB_PY.comprobacionCondicional(a, true, 3, aleft, aright);
    :}
    | operaciones_logicas:e                             {:parser.mc.gotoEscribir(); RESULT = e;:}
;

ppa ::=
    PARENTESIS_A
    | error:x
    {:parser.msVB_PY.errorSintax(xleft, xright, x, "Error sintactico en la instruccion indicada. Se esperaba el token: << ( >>, no el token indicado.");:}
;

/********************************** Concatenacion *************************************/

concatenacion ::=
    concatenacion:a MAS concatenacion:e                         {:parser.msVB_PY.concatencacion(a, e);:}
    | concatenacion:a COMA concatenacion:e                      {:parser.msVB_PY.concatencacion(a, e);:}
    | CADENA:e                                                  {:parser.mc.imprimir(new Simbolo(null, e)); RESULT = e;:}
    | estruct:e                                                 {:parser.mc.imprimirPrintf(0, (Simbolo) e, -1); RESULT = e;:}
    | error:e                                                   
    {:parser.msVB_PY.errorSintax(eleft, eright, e, "Error sintactico en la concatenacion de cadenas, de la instruccion de imprimir mensajes en la terminal. Se esperaba una concatenacion de cadenas.");:}
;

/******************************** Mensaje en Pantalla *********************************/

mensaje_terminal ::=
    PRINT ppa concatenacion:e PARENTESIS_C              {:parser.mc.imprimirNuevaLinea(); RESULT = e;:}
    | error:o ppa concatenacion:e PARENTESIS_C            
    {:
        parser.msVB_PY.errorSintax(oleft, oright, o, "Error sintactico en la intruccion de mensaje terminal. Se esperaba el token: << print >>");
        parser.mc.imprimirNuevaLinea(); RESULT = e;
    :}
    | PRINT ppa concatenacion:e error:o                   
    {:
        parser.msVB_PY.errorSintax(oleft, oright, o, "Error sintactico en la intruccion de mensaje terminal. Se esperaba el token: << ) >>");
        parser.mc.imprimirNuevaLinea(); RESULT = e;
    :}
;

/***************************** Asignacion de Variables ********************************/

asignacion_variables ::=
    ID:a IGUAL asignacion:e          
    {:  
        if(e != null) {
            RESULT = new Simbolo(((Simbolo) e).getTipo(), ((Simbolo) e), a);
        } else { 
            RESULT = e;
        }
    :}
    | ID:a error:o asignacion:e          
    {:  
        parser.msVB_PY.errorSintax(oleft, oright, o, "Error sintactico en la intruccion asignacion de variables. Se esperaba el token: << = >>");
        if(e != null) {
            RESULT = new Simbolo(((Simbolo) e).getTipo(), ((Simbolo) e), a);
        } else { 
            RESULT = e;
        }
    :}
    | error:o IGUAL asignacion:e          
    {:  
       parser.msVB_PY.errorSintax(oleft, oright, o, "Error sintactico en la intruccion asignacion de variables. Se esperaba el token: << Identificador >>");
    :}
;

asignacion ::=
    return_dato:e                   {:RESULT = e;:}
    | solicitud_datos:e             {:RESULT = e;:}
    | error:e
    {:parser.msVB_PY.errorSintax(eleft, eright, e, "Error sintactico en la instruccion de valores a asignar. Se esperaba: << Identificador, Numero, Operacion Aritmetica >>.");:}
;

/******************************* Solicitud de Datos ***********************************/

solicitud_datos ::=
    IINPUT:e solicitud_p                {:RESULT = new Simbolo(Constantes.INT_VAR_VB_PY, 0, 1);:}
    | FINPUT:e solicitud_p              {:RESULT = new Simbolo(Constantes.FLOAT_VAR_VB_PY, 0, 3);:}
    | CINPUT:e solicitud_p              {:RESULT = new Simbolo(Constantes.CHAR_VAR_VB_PY, '\b', 2);:}
;

solicitud_p ::=
    PARENTESIS_A CADENA:e PARENTESIS_C             {:parser.mc.imprimir(new Simbolo(null, e));:}
    | PARENTESIS_A PARENTESIS_C
    | error:e PARENTESIS_C
    {:parser.msVB_PY.errorSintax(eleft, eright, e, "Error sintactico en la instruccion solicitud de datos. Se esperaba el token: << ) >>.");:}
    | error:e 
    {:parser.msVB_PY.errorSintax(eleft, eright, e, "Error sintactico en la instruccion solicitud de datos. Se esperaba una estructura como: << ( Texto ) >>.");:}
;

/************************************* While ******************************************/

while_n ::=
    WHILE                               {:RESULT = new While(parser.mc.addWhile(), null);:}
;

while_p ::=
    while_n:w condicional:a DOS_PUNTOS INDENT
    {:
        parser.mc.nuevoAnalisisBoolean();
        parser.mc.escribirEtiquetasInicio(false);
        parser.mc.addEtiquetasEstructuras();
        ((While) w).setA(a);
        RESULT = w;
    :}
    | while_n:w condicional:a DOS_PUNTOS error:e INDENT
    {:
        parser.msVB_PY.errorSintax(eleft, eright, e, "Error sintactico en la instrucion while. Se esperaba el token: << Indent >>, no el token indicado.");
        parser.mc.nuevoAnalisisBoolean();
        parser.mc.escribirEtiquetasInicio(false);
        parser.mc.addEtiquetasEstructuras();
        ((While) w).setA(a);
        RESULT = w;
    :}
;

while ::= 
    while_p:a
        estructuras_p:e 
        SALTO                                       
        {:
            RESULT = parser.msVB_PY.contruirEstructuraCiloDW(e, ((While) a).getA(), 1);
            parser.mc.finWhile(((While) a).getIdEtiqueta());
            parser.mc.imprimirResto();
        :}
    | while_p:a
        estructuras_p:e                                 
        DEDENT                                      
        {:
            RESULT = parser.msVB_PY.contruirEstructuraCiloDW(e, ((While) a).getA(), 1);
            parser.mc.finWhile(((While) a).getIdEtiqueta());
            parser.mc.imprimirResto();
        :}
;

/************************************* For ********************************************/

var ::=
    ID:e                    {:RESULT = parser.msVB_PY.idPY((String) e, eleft, eright);:}
    | error:e
    {:parser.msVB_PY.errorSintax(eleft, eright, e, "Error sintactico en la declaracion de datos, de la instruccion for. Se esperaba el token: << Identificador >>");:}
;

for_p ::=
    FOR var:a IN RANGE ppa rango:e PARENTESIS_C DOS_PUNTOS INDENT               {:RESULT = new For(e, (Simbolo) a, parser.mc.addForPY(a, e));:}
    | FOR var:a IN RANGE ppa rango:e error:x DOS_PUNTOS INDENT
    {:
        parser.msVB_PY.errorSintax(xleft, xright, x, "Error sintactico en la instruccion de la estructura for. Se esperaba el token: << ) >>, no el token indicado.");
        RESULT = new For(e, (Simbolo) a, parser.mc.addForPY(a, e));
    :}
    | FOR var:a error:x IN RANGE ppa rango:e PARENTESIS_C DOS_PUNTOS INDENT
    {:
        parser.msVB_PY.errorSintax(xleft, xright, x, "Error sintactico en la instruccion de la estructura for. Se esperaba el token: << in >>, no el token indicado.");
        RESULT = new For(e, (Simbolo) a, parser.mc.addForPY(a, e));
    :}
    | FOR var:a IN error:x RANGE ppa rango:e PARENTESIS_C DOS_PUNTOS INDENT
    {:
        parser.msVB_PY.errorSintax(xleft, xright, x, "Error sintactico en la instruccion de la estructura for. Se esperaba el token: << range >>, no el token indicado.");
        RESULT = new For(e, (Simbolo) a, parser.mc.addForPY(a, e));
    :}
    | error:x var:a IN RANGE ppa rango:e PARENTESIS_C DOS_PUNTOS INDENT
    {:
        parser.msVB_PY.errorSintax(xleft, xright, x, "Error sintactico en la instruccion de la estructura for. Se esperaba el token: << for >>, no el token indicado.");
        RESULT = new For(e, (Simbolo) a, parser.mc.addForPY(a, e));
    :}
;

for ::=
    for_p:e
        estructuras_p:i
        SALTO                                                                   
    {:
        RESULT = parser.msVB_PY.cicloForPY("", eleft, eright, ((For) e).getRango(), i);
        parser.mc.finFor(((For) e).getLista());
    :}
    | for_p:e
        estructuras_p:i
    DEDENT                                                                      
    {: 
        RESULT = parser.msVB_PY.cicloForPY("", eleft, eright, ((For) e).getRango(), i);
        parser.mc.finFor(((For) e).getLista());
    :}
;

rango ::=
    return_dato:a                               {:RESULT = a;:}
    | return_dato:a COMA return_dato:e          
    {:
        List<Simbolo> lista = new ArrayList<>();
        if(a != null) {
            lista.add((Simbolo) a);
        } 
        if(e != null) {
            lista.add((Simbolo) e);
        }
        RESULT = lista;
    :}
    | return_dato:a COMA return_dato:e COMA return_dato:i
    {:
        List<Simbolo> lista = new ArrayList<>();
        if(a != null) {
            lista.add((Simbolo) a);
        } 
        if(e != null) {
            lista.add((Simbolo) e);
        }
        if(e != null) { 
            lista.add((Simbolo) i); 
        }
        RESULT = lista;
    :}
    | error:x
    {:parser.msVB_PY.errorSintax(xleft, xright, x, "Error sintactico en la instruccion de la estructura for. Se esperaba el token: << Identificador | Opearcion Aritmetica >>, no el token indicado.");:}
;

/************************************ if **********************************************/

if ::=
    ii:a DOS_PUNTOS INDENT
        estructuras_p:e
    DEDENT                                      {:RESULT = parser.msVB_PY.crearEstructuraCondicional(e, a, 1); parser.mc.imprimirResto();:}
    | ii:a DOS_PUNTOS INDENT
        estructuras_p:e                   
        SALTO                                   {:RESULT = parser.msVB_PY.crearEstructuraCondicional(e, a, 1); parser.mc.imprimirResto();:}
    | ii:a DOS_PUNTOS error:x INDENT
        estructuras_p:e
    DEDENT                                      
    {:
        parser.msVB_PY.errorSintax(xleft, xright, x, "Error sintactico en la instruccion de la estructura if. Se esperaba el token: << Indent >>, no el token indicado.");
        RESULT = parser.msVB_PY.crearEstructuraCondicional(e, a, 1); parser.mc.imprimirResto();
    :}
    | ii:a DOS_PUNTOS error:x INDENT
        estructuras_p:e                   
        SALTO                                   
    {:
        parser.msVB_PY.errorSintax(xleft, xright, x, "Error sintactico en la instruccion de la estructura if. Se esperaba el token: << Indent >>, no el token indicado.");
        RESULT = parser.msVB_PY.crearEstructuraCondicional(e, a, 1); parser.mc.imprimirResto();
    :}
;
 
ii ::=
    IF condicional:e                        {:parser.mc.escribirEtiquetasInicio(false); parser.mc.escribirEtiquetaFin(); parser.mc.nuevoAnalisisBoolean(); RESULT = e;:}
;
  
ie ::=
    ELIF condicional:e                      {:parser.mc.escribirEtiquetasInicio(false); parser.mc.escribirEtiquetaFin(); parser.mc.nuevoAnalisisBoolean(); RESULT = e;:}
;

ee ::=
    ELSE                                    {:parser.mc.escribirEtiquetasInicio(false); parser.mc.escribirEtiquetaFin(); parser.mc.nuevoAnalisisBoolean(); :}
;

else_if ::=
    ie:e DOS_PUNTOS INDENT
        estructuras_p:a
    DEDENT                                      {:RESULT = parser.msVB_PY.crearEstructuraCondicional(a, e, 2); parser.mc.imprimirResto();:}
    | ie:e DOS_PUNTOS INDENT
        estructuras_p:a                         
        SALTO                                   {:RESULT = parser.msVB_PY.crearEstructuraCondicional(a, e, 2); parser.mc.imprimirResto();:}
    | ie:e DOS_PUNTOS error:x INDENT
        estructuras_p:a
    DEDENT                                      
    {:
        parser.msVB_PY.errorSintax(xleft, xright, x, "Error sintactico en la instruccion de la estructura elif. Se esperaba el token: << Indent >>, no el token indicado.");
        RESULT = parser.msVB_PY.crearEstructuraCondicional(a, e, 2); parser.mc.imprimirResto();
    :}
    | ie:e DOS_PUNTOS error:x INDENT
        estructuras_p:a                         
        SALTO                                   
    {:
        parser.msVB_PY.errorSintax(xleft, xright, x, "Error sintactico en la instruccion de la estructura elif. Se esperaba el token: << Indent >>, no el token indicado.");
        RESULT = parser.msVB_PY.crearEstructuraCondicional(a, e, 2); parser.mc.imprimirResto();
    :}
;

else ::=
    ee DOS_PUNTOS INDENT
        estructuras_p:a                         
    DEDENT                                      {:RESULT = parser.msVB_PY.crearEstructuraCondicional(a, false, 3); parser.mc.imprimirResto();:}                         
    | ee DOS_PUNTOS INDENT
        estructuras:a                           
        SALTO                                   {:RESULT = parser.msVB_PY.crearEstructuraCondicional(a, false, 3); parser.mc.imprimirResto();:}
    | ee DOS_PUNTOS error:x INDENT
        estructuras_p:a                         
    DEDENT                                      
    {:
        parser.msVB_PY.errorSintax(xleft, xright, x, "Error sintactico en la instruccion de la estructura else. Se esperaba el token: << Indent >>, no el token indicado.");
        RESULT = parser.msVB_PY.crearEstructuraCondicional(a, false, 3); parser.mc.imprimirResto();
    :}
    | ee DOS_PUNTOS error:x INDENT
        estructuras:a                           
        SALTO                                   
    {:
        parser.msVB_PY.errorSintax(xleft, xright, x, "Error sintactico en la instruccion de la estructura else. Se esperaba el token: << Indent >>, no el token indicado.");
        RESULT = parser.msVB_PY.crearEstructuraCondicional(a, false, 3); parser.mc.imprimirResto();
    :}
;

else_if_p ::=
    else_if:e else_if_p:a   
    {:
        List<Simbolo> listaSimbolos = new ArrayList<>();
        if(a != null) {
            if(a instanceof Simbolo) {
                listaSimbolos.add((Simbolo) a);
            } else {
                listaSimbolos = (List<Simbolo>) a;
            }
        }
        if(e != null) {
            listaSimbolos.add((Simbolo) e);
        }
        RESULT = listaSimbolos;
    :}    
    |                                           {:RESULT = null;:}
;

/************************** estructura condicional if *********************************/

estructura_if ::=
    if:a else_if_p:e                            {:RESULT = parser.msVB_PY.construirCondicionalIf(a, e, null);:} 
    | if:a else_if_p:e else:i                   {:RESULT = parser.msVB_PY.construirCondicionalIf(a, e, i);:} 
;

/********************************** DEF *******************************************/

def_p ::=
    DEF ID:a PARENTESIS_A parametros:e PARENTESIS_C DOS_PUNTOS INDENT   
    {:parser.mc.addCuartetoProcedimiento(2, a, "PY", e, Constantes.FLOAT_VAR_VB_PY);  RESULT = new Procedimiento(a, e);:}
    | DEF ID:a PARENTESIS_A parametros:e PARENTESIS_C DOS_PUNTOS error:x INDENT   
    {:
        parser.msVB_PY.errorSintax(xleft, xright, x, "Error sintactico en la instruccion de la estructura def. Se esperaba el token: << Indent >>, no el token indicado.");
        parser.mc.addCuartetoProcedimiento(2, a, "PY", e, Constantes.FLOAT_VAR_VB_PY);  RESULT = new Procedimiento(a, e);
    :}
    | DEF ID:a error:x PARENTESIS_A parametros:e PARENTESIS_C DOS_PUNTOS INDENT   
    {:
        parser.msVB_PY.errorSintax(xleft, xright, x, "Error sintactico en la instruccion de la estructura def. Se esperaba el token: << ( >>, no el token indicado.");
        parser.mc.addCuartetoProcedimiento(2, a, "PY", e, Constantes.FLOAT_VAR_VB_PY);  RESULT = new Procedimiento(a, e);
    :}
    | DEF ID:a error:x parametros:e PARENTESIS_C DOS_PUNTOS INDENT   
    {:
        parser.msVB_PY.errorSintax(xleft, xright, x, "Error sintactico en la instruccion de la estructura def. Se esperaba el token: << ( >>, no el token indicado.");
        parser.mc.addCuartetoProcedimiento(2, a, "PY", e, Constantes.FLOAT_VAR_VB_PY);  RESULT = new Procedimiento(a, e);
    :}
;

def ::= //Comprobar si existe un return con los niveles 
    def_p:e
        estructuras_p:i
        DEDENT                                  
        {:  
            parser.lexer.limpiarStack(); 
            RESULT = parser.msVB_PY.def(((Procedimiento) e).getU(), ((Procedimiento) e).getE(), i, ileft, iright);
        :}
    | def_p:e
        estructuras_p:i                         
        {:
            parser.lexer.limpiarStack(); 
            RESULT = parser.msVB_PY.def(((Procedimiento) e).getU(), ((Procedimiento) e).getE(), i, ileft, iright);
        :}
;

space ::=
    SALTO
    |
;

parametros ::=
    parametros_p:e                          {:RESULT = e;:}
   |                                       {:RESULT = new ArrayList<>();:}
;

parametros_p ::=
    ID:e                               {:RESULT = parser.msVB_PY.parametroPY(e, eleft, eright);:}
    | parametros_p:a COMA ID:e 
    {:
        Simbolo s = parser.msVB_PY.parametroPY(e, eleft, eright);
        List<Simbolo> listaSimbolos = new ArrayList<>();
        if(a != null) {
            if(a instanceof Simbolo) {
                listaSimbolos.add((Simbolo) a);
            } else {
                listaSimbolos = (List<Simbolo>) a;
            }
        }
        if(s != null) {
            listaSimbolos.add((Simbolo) s);
        }
        RESULT = listaSimbolos;
    :}
    | error:e                       
    {: 
        RESULT = new ArrayList<>();
        parser.msVB_PY.errorSintax(eleft, eright, e, "Error sintactico en la instruccion de parametros, no se esperaba el token indicado. Se espera una estructura: << Param, Param_n ... >>.");
    :}
;

llamada_fp ::=
    ID:a PARENTESIS_A param:e PARENTESIS_C 
    {:RESULT = parser.mbm.buscarMetodo("PY_", a, e, aleft, aright, 2, true);:}
    | error:x PARENTESIS_A param:e PARENTESIS_C 
    {:
        parser.msVB_PY.errorSintax(xleft, xright, x, "Error sintactico en la instruccion del llamado a una funcion. Se esperaba el token: << Identificador >>, no el token indicado.");
    :}
;

param ::=
    operaciones_aritmeticas:e param_p:a                 
    {:
        List<Simbolo> lista = new ArrayList<>();
        if (e != null) {
            lista.add((Simbolo) e);
        }
        if(a instanceof Simbolo) {
            lista.add((Simbolo) a);
        } else {
            lista.addAll((List<Simbolo>) a);
        }
        RESULT = lista;
    
    :} 
    | error:e     
    {: 
        parser.msVB_PY.errorSintax(eleft, eright, e, "Error sintactico en la instruccion de llamada a una funcion, no se esperaba el token indicado para el reconocimiento de parametros. Se esperaba el token: << Param, Param_n ... >>");
    :}
    | operaciones_aritmeticas:e                         {:RESULT = e;:} 
    | 
;

param_p ::=
    COMA operaciones_aritmeticas:e                     {:RESULT = e;:}
    | param_p:a COMA operaciones_aritmeticas:e         {:RESULT = parser.msVB_PY.lista(a, e);:}
    | error:e param_p:a
    {: 
        RESULT = a;
        parser.msVB_PY.errorSintax(eleft, eright, e, "Error sintactico en la instruccion de llamada a una funcion, no se esperaba el token indicado para el reconocimiento de parametros. Se esperaba el token: << Param, Param_n ... >>");
    :}
;

estructuras ::=
    mensaje_terminal space                              {:/* Ignore */:}
    | asignacion_variables:e space                      {:parser.msVB_PY.declararVaiablesPY(eleft, eright, (Simbolo) e);:}
    | while:e                                           {:RESULT = e;:}
    | for:e                                             {:RESULT = e;:}
    | estructura_if:e                                   {:RESULT = e;:}
    | return:e space                                    {:RESULT = e;:}
    | llamada_fp space                                            
    {:
        Object as = null;
        parser.mc.addYRemovePuntero(as, false);
    :}
    | error:e SALTO                                     
    {:parser.msVB_PY.errorSintax(eleft, eright, e, "Error en el bloque de instrucciones de la estructura, por favor revisa la entrada en la linea y columna indicada. \nEstructura esperada: << print || while || for || Asignacion de variables || If >>, se recupero hasta: << Salto de Linea >>");:}
    | error:e DEDENT                                        
    {:parser.msVB_PY.errorSintax(eleft, eright, e, "Error en el bloque de instrucciones de la estructura, por favor revisa la entrada en la linea y columna indicada. \nEstructura esperada: << print || while || for || Asignacion de variables || If >>, se recupero hasta: << dedent >>");:}
;

estructuras_p ::=
    estructuras:e estructuras_p:a                       {:RESULT = parser.msVB_PY.simbolosReturn(a, e, aleft, aright);:}
    | estructuras:e                                     {:if(e == null) RESULT = new ArrayList<Simbolo>(); else RESULT = e;:}
;